program     -> declaration* EOF ;

declaration -> fnDecl
            | letDecl
            | statement ;

fnDecl    -> "fn" function;
function  -> IDENTIFIER "(" parameters? ")" block ;
parameters â†’ IDENTIFIER ( "," IDENTIFIER )* ;

letDecl   -> "let" IDENTIFIER ( "=" expression )? ";" ;

statement   -> assignment
            | exprStmt
            | ifStmt
            | printStmt
            | whileStmt
            | forStmt
            | loopStmt
            | block 
            | breakStmt
            | continueStmt ;

assignment -> IDENTIFIER "=" expression ";" ;
exprStmt  -> expression ";" ;
ifStmt    -> "if" expression block ( "else" block )? ;
printStmt -> "print" expression ";" ;
whileStmt -> "while" expression block ;
forStmt   -> "for" IDENTIFIER "in" RANGE | RANGECLOSED block ;
loopStmt  -> "loop" block ;
block     -> "{" declaration* "}" ;
breakStmt -> "break" ;
continueStmt -> "continue" ;

expression     -> logic_or ;
logic_or       -> logic_and ( "||" logic_and )* ;
logic_and      -> equality ( "&&" equality )* ;
equality       -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison     -> addition ( ( ">" | ">=" | "<" | "<=" ) addition )* ;
addition       -> multiplication ( ( "-" | "+" ) multiplication )* ;
multiplication -> unary ( ( "/" | "*" ) unary )* ;
unary          -> ( "!" | "-" ) unary | call ;
call           -> primary ( "(" arguments? ")" )* ;
arguments      -> expression ( "," expression )* ;
primary        -> NUMBER | STRING | "false" | "true" | "nil"
               | "(" expression ")" ;